{
  function removeSpace(arr) {
    for (var i = 0; i < arr.length; i++) {
      if (arr[i] === " ") {
        arr.splice(i, 1);
      }
    }
  }
}

S =
  S:(CALLING / SUBJ_NP / SUBJ_VP / SUBJ / YES_NO) _ EXCLAM? { return ["S", S]; }

SUBJ_NP = SUBJ? NP
SUBJ_VP = SUBJ? VP

YES_NO = YES_NO:(YES / NO) { return ["YES_NO", YES_NO]; }

YES =
  YES:("dung roi" / "dung the" / "dung" / "uh" / "okay" / "ok" / "yup" / "yeah" / "yes") _ { return ["YES", YES ]; }

NO =
  NO:("khong phai" / "khong" / "nham roi" / "sai roi" / "sai") { return ["NO", NO ]; }

SUBJ = SUBJ:NP { return ["SUBJ", SUBJ]; }

// verb phrase
VP
  = VP:(PP? V PP? NP? PP?) { return ["VP", VP]; }

// noun phrase
NP
  = NP:(NUMBER? CLASSIFIER? (NOUN / PRP) ADJ* / NUMBER) _
  {
    return ["NP", NP];
  }

// động từ tình thái
VTT = VTT:("muon") _

// động từ hành động
VHD =
  VHD:("dat lenh" / "la" / "dat mua" / "mua" / "ban" / "huy" / "sua" / "chao") _ { return VHD; }

V = VTT? V:VHD { return ["V", V]; }

// Prepositional phrase
PP =
  PP:(P / ("cho" / "giup") $_ PRP / "gia" $_ PRICE:NUMBER {return ["PRICE", PRICE]; }) _ {
  removeSpace(PP);
  return ["PP", PP];
}

P =
  P:("nhanh" / "ngay") _ { return {P: P}; }

CLASSIFIER = CLASSIFIER:("con") _ { return {CLASSIFIER: CLASSIFIER}; }

NOUN =
  NOUN:(STOCK / "gia" / "ma" / "lenh") _ { return ["NOUN", NOUN]; }

STOCK =
  ("co phieu" / "ma" / "cp")? _ STOCK:(<%= stockSymbols %>) _ { return ["STOCK", STOCK]; }

ADJ =
  ADJ:("vua xong" / "dep") _ { return ["ADJ", ADJ]; }

NUMBER
    = NUMBER:number $_ ("ngan" / "nghin" / "k") _ { NUMBER *= 1000; return ["NUMBER", NUMBER]; }
    / NUMBER:number _ { return ["NUMBER", NUMBER]; }

// Pronoun
PRP =
  PRP:("em yeu" / "toi" / "tao" / "anh" / "em" / "chi" / "minh" / "bo may" / "co" / "di" / "chu" / "bac" / "chau" / "may") _ { return ["PRP", PRP]; }

EXCLAM =
  EXCLAM:("nhe"/ "voi" / "day" / "di" / "dau") { return ["EXCLAM", EXCLAM]; }

CALLING =
  CALLING:(PRP $_ ("oi" / "ah" / "a")) _ { return ["CALLING", CALLING]; }

_ = [ \t\n\r]*
__ = [ \t\n\r]+


number "number"
  = minus? int frac? exp? { return parseFloat(text()); }

decimal_point = "."
digit1_9      = [1-9]
e             = [eE]
exp           = e (minus / plus)? DIGIT+
frac          = decimal_point DIGIT+
int           = zero / (digit1_9 DIGIT*)
minus         = "-"
plus          = "+"
zero          = "0"

DIGIT = [0-9]
